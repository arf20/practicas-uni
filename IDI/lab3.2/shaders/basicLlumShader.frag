#version 330 core

in vec4 vertexSCO;
in vec3 normalSCO;

in vec3 _matamb;
in vec3 _matdiff;
in vec3 _matspec;
in float _matshin;

uniform vec3 colorFocus;
uniform vec3 posFocus;  // en SCO

vec3 llumAmbient = vec3(0.2, 0.2, 0.2);

out vec4 FragColor;

vec3 Ambient() {
    return llumAmbient * _matamb;
}

vec3 Difus(vec3 NormSCO, vec3 L, vec3 colFocus) {
    // Tant sols retorna el terme difús
    // S'assumeix que els vectors que es reben com a paràmetres estan normalitzats
    vec3 colRes = vec3(0);
    // Càlcul component difusa, si n'hi ha
    if (dot (L, NormSCO) > 0)
      colRes = colFocus * _matdiff * dot (L, NormSCO);
    return (colRes);
}

vec3 Especular(vec3 NormSCO, vec3 L, vec4 vertSCO, vec3 colFocus) {
    // Tant sols retorna el terme especular!
    // Els vectors rebuts com a paràmetres (NormSCO i L) estan normalitzats
    vec3 colRes = vec3 (0);
    // Si la llum ve de darrera o el material és mate no fem res
    if ((dot(NormSCO,L) < 0) || (_matshin == 0))
      return colRes;  // no hi ha component especular

    // Calculem R i V
    vec3 R = reflect(-L, NormSCO); // equival a: 2.0*dot(NormSCO,L)*NormSCO - L;
    vec3 V = normalize(-vertSCO.xyz); // perquè la càmera està a (0,0,0) en SCO

    if (dot(R, V) < 0)
      return colRes;  // no hi ha component especular
    
    // Calculem i retornem la component especular
    float shine = pow(max(0.0, dot(R, V)), _matshin);
    return (_matspec * colFocus * shine); 
}

void main() {
    vec3 normalnormalSCO = normalize(normalSCO);
    vec3 L = normalize(posFocus - vec3(vertexSCO));
	FragColor = vec4(Ambient() + Difus(normalnormalSCO, L, colorFocus) + Especular(normalnormalSCO, L, vertexSCO, colorFocus), 1.0);
}

